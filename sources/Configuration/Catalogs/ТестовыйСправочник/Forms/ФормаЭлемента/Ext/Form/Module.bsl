
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ЦентрМониторинга
	// Подсчитываем количество созданий формы, стандартный разделитель ".".
	Комментарий = Строка(ПолучитьСкоростьКлиентскогоСоединения());
	ЦентрМониторинга.ЗаписатьОперациюБизнесСтатистики(ЭтаФорма.ИмяФормы + ".ПриСозданииНаСервере", 1, Комментарий);
	// Конец СтандартныеПодсистемы.ЦентрМониторинга
	
	// СтандартныеПодсистемыИТГ.СтруктурированныеРеквизиты
	СтруктурированныеРеквизиты.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемыИТГ.СтруктурированныеРеквизиты
	
	// СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	//СклонениеПредставленийОбъектов.ПриСозданииНаСервере(ЭтотОбъект,
	//	Объект[ЕдиницыВеличинКлиентСерверПовтИсп.ПолучитьРеквизитНаименования()]);
	// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемыИТГ.СтруктурированныеРеквизиты
	СтруктурированныеРеквизиты.ПередЗаписьюНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемыИТГ.СтруктурированныеРеквизиты
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПриОткрытииСтруктурированногоЗначения(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормыСтруктурированногоЗначения = ПодготовитьПараметрыФормыСтруктурированногоЗначения(Элемент.Имя);
	ОповещениеОЗакрытииФормыСтруктурированногоЗначения = Новый ОписаниеОповещения(
		"ПриОткрытииСтруктурированногоЗначенияЗавершение", ЭтотОбъект,
		Новый Структура("ИмяЭлемента", Элемент.Имя));
	ОткрытьФорму("ОбщаяФорма.ЧислоВЭкспоненциальнойЗаписи", ПараметрыФормыСтруктурированногоЗначения,
		ЭтаФорма, Строка(ЭтаФорма.УникальныйИдентификатор) + Элемент.Имя, , ,
		ОповещениеОЗакрытииФормыСтруктурированногоЗначения);
	
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПодготовитьПараметрыФормыСтруктурированногоЗначения(Знач ИмяЭлемента)
	
	Элемент = ЭтаФорма.Элементы[ИмяЭлемента];
	ЧастиПутиКДанным = СтрРазделить(Элемент.ПутьКДанным, ".");
	
	ПараметрыФормыСтруктурированногоЗначения = Новый Структура;
	
	ЗаголовокФормыСтруктурированногоЗначения = "";
	Если Не ПустаяСтрока(Элемент.Заголовок) Тогда
		ЗаголовокФормыСтруктурированногоЗначения = Элемент.Заголовок;
	Иначе
		РеквизитФормы = ЭтаФорма.ПолучитьРеквизиты(ЧастиПутиКДанным[0])[0];
		Если ЧастиПутиКДанным.Количество() = 1 Тогда
			ЗаголовокФормыСтруктурированногоЗначения = РеквизитФормы.Заголовок;
		Иначе
			МетаданныеРеквизита = Метаданные.НайтиПоТипу(РеквизитФормы.ТипЗначения.Типы()[0]); 
			Для ИндексЧастиПути = 1 по ЧастиПутиКДанным.ВГраница() Цикл 
				МетаданныеРеквизита = МетаданныеРеквизита.Реквизиты[ЧастиПутиКДанным[ИндексЧастиПути]];
			КонецЦикла;
			ЗаголовокФормыСтруктурированногоЗначения = МетаданныеРеквизита.Синоним;
		КонецЕсли;
	КонецЕсли;
	ПараметрыФормыСтруктурированногоЗначения.Вставить("Заголовок", ЗаголовокФормыСтруктурированногоЗначения);
	
	ТекущееЗначение = ЭтаФорма;
	Для Каждого ИмяРеквизита Из ЧастиПутиКДанным Цикл 
		ВладелецЗначения = ТекущееЗначение;
		ТекущееЗначение  = ТекущееЗначение[ИмяРеквизита];
	КонецЦикла;
	ПараметрыФормыСтруктурированногоЗначения.Вставить("Данные", ЭтаФорма[ИмяРеквизита + "Данные"]);
	
	Возврат ПараметрыФормыСтруктурированногоЗначения;
	
КонецФункции
	
&НаКлиенте
Процедура ПриОткрытииСтруктурированногоЗначенияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ИзменитьРеквизитФормыСоСтруктурированнымЗначением(ДополнительныеПараметры.ИмяЭлемента, Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьРеквизитФормыСоСтруктурированнымЗначением(Знач ИмяЭлемента, Знач Значение)
	
	Элемент = ЭтаФорма.Элементы[ИмяЭлемента];
	ЧастиПути = СтрРазделить(Элемент.ПутьКДанным, ".");
	ТекущееЗначение = ЭтаФорма;
	Для Каждого ИмяРеквизита Из ЧастиПути Цикл 
		ВладелецЗначения = ТекущееЗначение;
		ТекущееЗначение  = ТекущееЗначение[ИмяРеквизита];
	КонецЦикла;
	
	ЭтаФорма[ИмяРеквизита + "Данные"] = Значение;
	ВладелецЗначения[ИмяРеквизита] = ЧислаКлиентСервер.ПолучитьПредставлениеЧисла(Значение);
	
	ЭтаФорма.Модифицированность = Истина;
		
КонецПроцедуры

#КонецОбласти
