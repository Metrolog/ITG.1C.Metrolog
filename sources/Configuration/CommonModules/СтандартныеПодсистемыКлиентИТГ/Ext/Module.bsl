////////////////////////////////////////////////////////////////////////////////
// Подсистема "Стандартные подсистемы ИТГ".
// ОбщийМодуль.СтандартныеПодсистемыКлиентИТГ.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает параметры получения данных выбора по аналогии с параметром ПараметрыПолученияДанных
// обработчика события АвтоПодбор.
//
// Параметры:
// 	Форма - Форма - Форма, для элемента управления которой необходимо получить параметры получения данных.
// 	Элемент - Поле - Элемент управления, для которого необходимо получить параметры получения данных.
//
// Возвращаемое значение: Структура.
//
Функция ПолучитьПараметрыПолученияДанныхВыбора(Знач Форма, Знач Элемент) Экспорт
	
	Перем ПараметрыПолученияДанных;
	
	ПараметрыПолученияДанных = Новый Структура(
		"Отбор,СтрокаПоиска,ВыборГруппИЭлементов",
		Новый Структура,
		"",
		Элемент.ВыборГруппИЭлементов
	);

	Для Каждого СвязьПараметраВыбора Из Элемент.СвязиПараметровВыбора Цикл
		Если НЕ ПустаяСтрока(СвязьПараметраВыбора.Имя) Тогда
			ПараметрыПолученияДанных.Отбор.Вставить(
				?(ВРег(Лев(СвязьПараметраВыбора.Имя, 6)) = ВРег("Отбор."),
					Сред(СвязьПараметраВыбора.Имя, 7),
					СвязьПараметраВыбора.Имя),
				Вычислить("Форма." + СвязьПараметраВыбора.ПутьКДанным));
		КонецЕсли;
	КонецЦикла;

	Для Каждого ПараметрВыбора Из Элемент.ПараметрыВыбора Цикл
		Если НЕ ПустаяСтрока(ПараметрВыбора.Имя) Тогда
			ПараметрыПолученияДанных.Отбор.Вставить(
				?(ВРег(Лев(ПараметрВыбора.Имя, 6)) = ВРег("Отбор."),
					Сред(ПараметрВыбора.Имя, 7),
					ПараметрВыбора.Имя),
				ПараметрВыбора.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПараметрыПолученияДанных;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#КонецОбласти
