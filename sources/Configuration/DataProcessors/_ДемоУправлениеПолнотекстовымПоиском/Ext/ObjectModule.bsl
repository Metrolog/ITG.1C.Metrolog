#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает сведения о внешней обработке.
Функция СведенияОВнешнейОбработке() Экспорт
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.2.2.1");
	ПараметрыРегистрации.Информация = НСтр("ru = 'Обработка сервисных функций полнотекстового поиска. Используется для демонстрации возможностей подсистемы ""Дополнительные отчеты и обработки"".'");
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	ПараметрыРегистрации.Версия = "2.3.3.42";
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = НСтр("ru = 'Обновить индекс полнотекстового поиска'");
	Команда.Идентификатор = "ОбновитьИндекс";
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	Команда.ПоказыватьОповещение = Истина;
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = НСтр("ru = 'Вывод ошибки'");
	Команда.Идентификатор = "Исключение";
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода();
	
	Команда = ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление = НСтр("ru = 'Управление полнотекстовым поиском'");
	Команда.Идентификатор = "Управление";
	Команда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	Команда.ПоказыватьОповещение = Истина;
	
	Описание = НСтр("ru = 'Для управления индексом полнотекстового поиска требуется установка привилегированного режима.'");
	Разрешение = РаботаВБезопасномРежиме.РазрешениеНаИспользованиеПривилегированногоРежима(Описание);
	ПараметрыРегистрации.Разрешения.Добавить(Разрешение);
	
	Возврат ПараметрыРегистрации;
КонецФункции

// Интерфейс для запуска логики обработки.
Процедура ВыполнитьКоманду(Знач ИмяКоманды, Знач ПараметрыВыполнения) Экспорт
	Если ИмяКоманды = "Исключение" Тогда
		ВызватьИсключение НСтр("ru = 'Прикладной текст исключения'");
	КонецЕсли;
	Если ИмяКоманды = "Управление" Тогда
		ИмяКоманды = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыВыполнения, "ИмяКоманды");
	КонецЕсли;
	
	// Общие действия перед началом выполнения команд.
	УстановитьПривилегированныйРежим(Истина);
	
	// Диспетчеризация обработчиков команд.
	Если ИмяКоманды = "ОбновитьИндекс" Тогда
		ОбновитьИндекс();
	ИначеЕсли ИмяКоманды = "ОчиститьИндекс" Тогда
		ОчиститьИндекс();
	Иначе
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Команда ""%1"" не поддерживается.'"),
			ИмяКоманды);
	КонецЕсли;
	
	// Имитация длительной операции для демонстрации запуска фонового задания в клиент-серверном режиме.
	Если Не ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		ДатаОкончания = ТекущаяДатаСеанса() + 4;
		Пока ДатаОкончания > ТекущаяДатаСеанса() Цикл
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьИндекс()
	
	ПолнотекстовыйПоиск.ОбновитьИндекс(Ложь, Ложь);
	
КонецПроцедуры

Процедура ОчиститьИндекс()
	
	ПолнотекстовыйПоиск.ОчиститьИндекс();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли