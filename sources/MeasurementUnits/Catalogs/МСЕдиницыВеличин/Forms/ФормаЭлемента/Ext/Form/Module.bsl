

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	
	// СтандартныеПодсистемы.ЦентрМониторинга
	
	// Подсчитываем количество созданий формы, стандартный разделитель ".".
	Комментарий = Строка(ПолучитьСкоростьКлиентскогоСоединения());
	ЦентрМониторинга.ЗаписатьОперациюБизнесСтатистики("Справочник.МСЕдиницыВеличин.ПриСозданииНаСервере", 1, Комментарий);
	
	// Конец СтандартныеПодсистемы.ЦентрМониторинга
	
	// ОтложенноеОбновлениеПредставления
	
	МассивРеквизитов = Новый Массив;
	РеквизитНеобходимоИзменениеПредставления = Новый РеквизитФормы("НеобходимоИзменениеПредставления", Новый ОписаниеТипов("Булево"), , "Необходимо изменение представления");	
	МассивРеквизитов.Добавить(РеквизитНеобходимоИзменениеПредставления);
	ИзменитьРеквизиты(МассивРеквизитов);
	
	// Конец ОтложенноеОбновлениеПредставления
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВыражениеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока И НЕ Копирование Тогда
		Элемент.ТекущиеДанные.ПоказательСтепени=1;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыражениеПриИзменении(Элемент)
	
	// ОтложенноеОбновлениеПредставления
	
	НеобходимоИзменениеПредставления = Истина;
	ПодключитьОбработчикОжидания("Подключаемый_ИзменитьПредставление", 0.5, Истина);
	
	// Конец ОтложенноеОбновлениеПредставления
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ИзменитьПредставление() 
	НовыеЗначения = ПолучитьВычисляемыеРеквизиты();
	ЗаполнитьЗначенияСвойств(Объект, НовыеЗначения);
	НеобходимоИзменениеПредставления = Ложь;
КонецПроцедуры

#Область ЗапретРедактированияРеквизитовОбъектов

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
    ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

&НаСервере
Функция ПолучитьВычисляемыеРеквизиты()
	НовыеЗначения = Новый Структура();
	НовыеЗначения.Вставить("Обозначение", МСЕдиницыВеличинСервер.СформироватьОбозначениеЕдиницыВеличины(Объект));
	НовыеЗначения.Вставить("Наименование", МСЕдиницыВеличинСервер.СформироватьНаименованиеЕдиницыВеличины(Объект));	
	Возврат НовыеЗначения;
КонецФункции

#КонецОбласти
