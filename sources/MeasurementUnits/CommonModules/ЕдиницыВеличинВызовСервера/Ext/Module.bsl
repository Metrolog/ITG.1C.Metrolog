////////////////////////////////////////////////////////////////////////////////
// Подсистема "Метрологическая служба. Единицы величин".
// ОбщийМодуль.ЕдиницыВеличинВызовСервера.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

// Принудительно вычисляет зависимые реквизиты в переданном объекте Единицы Величины 
// или структуре, содержащей реквизиты единицы величины.
//
// Параметры:
//     Объект - Объект, Структура, ДанныеФормыСтруктура - объект либо структура,
//       хранящая данные объекта Единицы Величин
//
Процедура ОбновитьВычисляемыеРеквизиты(Объект) Экспорт
	
	Если Объект.ЭтоОсновнаяЕдиница Тогда
		
		Объект.ЭтоКогерентнаяЕдиница = Истина;
		
	Иначе
		
		Объект.ЭтоКогерентнаяЕдиница = Ложь;
		
		Для Каждого Множитель Из Объект.Выражение Цикл
			Объект.ЭтоКогерентнаяЕдиница = ЕдиницыВеличинСервер.ЭтоКогерентнаяЕдиница(Множитель.БазоваяЕдиница, Множитель.Приставка);
			Если НЕ Объект.ЭтоКогерентнаяЕдиница Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
			
	КонецЕсли;
	
	Если Объект.ЭтоКогерентнаяЕдиница Тогда
		Объект.Коэффициент = "1";
	КонецЕсли;
	
	Если Не Объект.ИмеетСпециальныеНаименованиеИОбозначение Тогда
		
		Объект.ДопустимоПрименениеПриставок = Ложь;
		
		Для Каждого ВариантОбозначения Из ЕдиницыВеличинКлиентСерверПовтИсп.ПолучитьВариантыОбозначенийЕдиницВеличин() Цикл
			
			Если ЕдиницыВеличинКлиентСерверПовтИсп.ИспользуютсяОбозначения(ВариантОбозначения) Тогда
				
				Объект[ЕдиницыВеличинКлиентСерверПовтИсп.ПолучитьРеквизитОбозначения(ВариантОбозначения)] = 
					ЕдиницыВеличинСервер.СформироватьОбозначениеЕдиницыВеличины(Объект, ВариантОбозначения);
				Объект[ЕдиницыВеличинКлиентСерверПовтИсп.ПолучитьРеквизитНаименования(ВариантОбозначения)] =
					ЕдиницыВеличинСервер.СформироватьНаименованиеЕдиницыВеличины(Объект, ВариантОбозначения);
					
			КонецЕсли;
				
		КонецЦикла;
			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
