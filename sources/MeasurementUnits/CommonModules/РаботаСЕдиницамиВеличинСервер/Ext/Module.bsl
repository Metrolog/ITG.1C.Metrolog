
#Область Обозначение

// Возвращает обозначение приставки.
//
// Параметры:
// 	Приставка	- Ссылка 		- Сссылка на объект приставки.
//
// Возвращаемое значение:
//  Строка - Обозначение приставки.
//
&НаСервере
Функция ПолучитьОбозначениеПриставки(Знач Приставка) Экспорт
	Если ЗначениеЗаполнено(Приставка) Тогда
		Возврат Приставка.Обозначение;
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции

// Возвращает обозначение базовой единицы величины.
//
// Параметры:
// 	Единица		- Ссылка 		- Сссылка на объект базовой единицы величины.
//
// Возвращаемое значение:
//  Строка - Обозначение базовой единицы величины.
//
&НаСервере
Функция ПолучитьОбозначениеЕдиницыВеличиныБазовой(Знач Единица) Экспорт
	Возврат Единица.Обозначение;
КонецФункции

// Возвращает обозначение единицы величины.
//
// Параметры:
// 	Единица		- Ссылка 		- Сссылка на объект единицы величины.
//
// Возвращаемое значение:
//  Строка - Обозначение единицы величины.
//
&НаСервере
Функция ПолучитьОбозначениеЕдиницыВеличины(Знач Единица) Экспорт
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		"РаботаСЕдиницамиВеличинСервер.СформироватьОбозначениеЕдиницыВеличины",
		"Единица",
		Единица,
		Новый ОписаниеТипов("СправочникСсылка.ЕдиницыВеличин, СправочникОбъект.ЕдиницыВеличин, ДанныеФормыСтруктура"));
	Возврат Единица.Обозначение;
КонецФункции

// Формирует обозначение единицы величины по её выражению из базовых.
//
// Параметры:
// 	Единица		- Ссылка 		- Сссылка на объект единицы величины.
//
// Возвращаемое значение:
//  Строка - Обозначение единицы величины.
//
&НаСервере
Функция СформироватьОбозначениеЕдиницыВеличины(Знач Единица) Экспорт
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		"РаботаСЕдиницамиВеличинСервер.СформироватьОбозначениеЕдиницыВеличины",
		"Единица",
		Единица,
		Новый ОписаниеТипов("СправочникСсылка.ЕдиницыВеличин, СправочникОбъект.ЕдиницыВеличин, ДанныеФормыСтруктура"));
	Обозначение = "";
	Если Единица <> Справочники.ЕдиницыВеличин.ПустаяСсылка() Тогда 
		Для каждого Множитель Из Единица.Выражение Цикл
			Если Строка(Множитель.ПоказательСтепени) <> "0" Тогда 
				ПредставлениеМножителя =
					РаботаСЕдиницамиВеличинСервер.ПолучитьОбозначениеПриставки(Множитель.Приставка)
					+ РаботаСЕдиницамиВеличинСервер.ПолучитьОбозначениеЕдиницыВеличиныБазовой(Множитель.БазоваяЕдиница)
					+ РаботаСоСтрокамиЮникодКлиентСервер.ПолучитьСтрокуИзПоказателяСтепениКраткую(Множитель.ПоказательСтепени)
				;
				Обозначение = ?(НЕ ПустаяСтрока(Обозначение),Обозначение + "⋅","") + ПредставлениеМножителя;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Возврат Обозначение;
КонецФункции

#КонецОбласти

#Область Наименование

// Возвращает приставку.
//
// Параметры:
// 	Приставка	- Ссылка 		- Сссылка на объект приставки.
//
// Возвращаемое значение:
//  Строка - приставка.
//
&НаСервере
Функция ПолучитьПриставку(Знач Приставка) Экспорт
	Если ЗначениеЗаполнено(Приставка) Тогда
		Возврат Приставка.Наименование;
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции

// Возвращает наименование базовой единицы величины.
//
// Параметры:
// 	Единица		- Ссылка 		- Сссылка на объект базовой единицы величины.
//
// Возвращаемое значение:
//  Строка - Наименование базовой единицы величины.
//
&НаСервере
Функция ПолучитьНаименованиеЕдиницыВеличиныБазовой(Знач Единица) Экспорт
	Возврат Единица.Наименование;
КонецФункции

// Возвращает наименование единицы величины.
//
// Параметры:
// 	Единица		- Ссылка 		- Сссылка на объект единицы величины.
//
// Возвращаемое значение:
//  Строка - Наименование единицы величины.
//
&НаСервере
Функция ПолучитьНаименованиеЕдиницыВеличины(Знач Единица) Экспорт
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		"РаботаСЕдиницамиВеличинСервер.СформироватьОбозначениеЕдиницыВеличины",
		"Единица",
		Единица,
		Новый ОписаниеТипов("СправочникСсылка.ЕдиницыВеличин, СправочникОбъект.ЕдиницыВеличин, ДанныеФормыСтруктура"));
	Возврат Единица.Наименование;
КонецФункции

// Формирует наименование единицы величины по её выражению из базовых.
//
// Параметры:
// 	Единица		- Ссылка 		- Сссылка на объект единицы величины.
//
// Возвращаемое значение:
//  Строка - Наименование единицы величины.
//
&НаСервере
Функция СформироватьНаименованиеЕдиницыВеличины(Знач Единица) Экспорт
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		"РаботаСЕдиницамиВеличинСервер.СформироватьОбозначениеЕдиницыВеличины",
		"Единица",
		Единица,
		Новый ОписаниеТипов("СправочникСсылка.ЕдиницыВеличин, СправочникОбъект.ЕдиницыВеличин, ДанныеФормыСтруктура"));
	Если Единица <> Справочники.ЕдиницыВеличин.ПустаяСсылка() Тогда 
		НаименованиеЧислителя = "";
		НаименованиеЗнаменателя = "";
		Предлог = "";
		Для Каждого Множитель Из Единица.Выражение Цикл
			МодульПоказателяСтепени = ?(Множитель.ПоказательСтепени < 0,
				-Множитель.ПоказательСтепени,
				Множитель.ПоказательСтепени
			);
			Падеж = ?(Множитель.ПоказательСтепени > 0, 1, 4);
			
			ПредставлениеМножителя =
				РаботаСЕдиницамиВеличинСервер.ПолучитьПриставку(Множитель.Приставка)
				+ СклонениеПредставленийОбъектов.ПросклонятьПредставление(
					РаботаСЕдиницамиВеличинСервер.ПолучитьНаименованиеЕдиницыВеличиныБазовой(Множитель.БазоваяЕдиница)
					, Падеж, Множитель.БазоваяЕдиница)
			;
			Если Множитель.БазоваяЕдиница.ИспользоватьНаименованиеСтепениКакПрилагательное
				И МодульПоказателяСтепени >= 2
				И МодульПоказателяСтепени <= 3
			Тогда 
				ПредставлениеМножителя =
					РаботаСоСтрокамиЮникодСерверПовтИсп.ПолучитьНаименованиеИзПоказателяСтепениКакПрилагательное(МодульПоказателяСтепени, Падеж)
					+ " " + ПредставлениеМножителя
				;
			ИначеЕсли МодульПоказателяСтепени >= 2 Тогда
				ПредставлениеМножителя = ПредставлениеМножителя + " "
					+ РаботаСоСтрокамиЮникодСерверПовтИсп.ПолучитьНаименованиеИзПоказателяСтепени(МодульПоказателяСтепени)
				;
			КонецЕсли;
			
			Если Множитель.ПоказательСтепени > 0 Тогда
				НаименованиеЧислителя = ?(НЕ ПустаяСтрока(НаименованиеЧислителя), НаименованиеЧислителя + "-", "") + ПредставлениеМножителя;
			ИначеЕсли Множитель.ПоказательСтепени < 0 Тогда
				НаименованиеЗнаменателя = ?(НЕ ПустаяСтрока(НаименованиеЗнаменателя), НаименованиеЗнаменателя + "-", "") + ПредставлениеМножителя;
				Если ПустаяСтрока(Предлог) И (МодульПоказателяСтепени = 1) Тогда
					Предлог = Множитель.БазоваяЕдиница.ПредлогЗнаменателяНаименованияСоставнойЕдиницыВеличины;
				Иначе
					Предлог = "на";
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Возврат 
			?(ПустаяСтрока(НаименованиеЧислителя), "единица", НаименованиеЧислителя)
			+ ?(ПустаяСтрока(НаименованиеЗнаменателя), "", " " + Предлог + " " + НаименованиеЗнаменателя)
		;
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции

#КонецОбласти