
#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Степени и индексы

// Преобразует переданный показатель степени (число) в Unicode строку (верхний индекс).
//
// Параметры:
//   ПоказательСтепени	 - Строка, Число	 - Показатель степени
// 
// Возвращаемое значение:
//   Строка - показатель степени как Unicode строка (символами из блока верхнего индекса)
//
Функция ПолучитьСтрокуИзПоказателяСтепени(Знач ПоказательСтепени) Экспорт
	
	Перем Результат, Соответствие;
	
	Результат = "";
	Соответствие = РаботаСоСтрокамиЮникодКлиентСерверПовтИсп.ПолучитьСоответствиеНадстрочныхИндексов();
	
	Для НомерСимвола = 1 По СтрДлина(ПоказательСтепени) Цикл
		Результат = Результат + Соответствие.Получить(Сред(ПоказательСтепени, НомерСимвола, 1));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Преобразует переданный показатель степени (число) в Unicode строку (верхний индекс).
//  Для показателя степени "1" возвращает пустую строку.
//
// Параметры:
//  ПоказательСтепени	 - Строка, Число	 - Показатель степени
// 
// Возвращаемое значение:
//   Строка - показатель степени как Unicode строка (символами из блока верхнего индекса)
//
Функция ПолучитьСтрокуИзПоказателяСтепениКраткую(Знач ПоказательСтепени) Экспорт
	
	Если Строка(ПоказательСтепени) = "1" Тогда
		Возврат "";
	Иначе
		Возврат ПолучитьСтрокуИзПоказателяСтепени(ПоказательСтепени);
	КонецЕсли;
	
КонецФункции

// Формирует соответствие шаблонов наименований степеней показателю степени.
//
// Параметры:
//   Объект - Строка, Ссылка, Объект - Представление основания степени
//   ПоказательСтепени - Строка, Число - Показатель степени (от 0 до 9)
//   ОбъектГеометрический - Булево - вернуть "квадратный метр" вместо "метр в квадрате"
//   КодЯзыка - Строка - Указывается код языка, на котором нужно получить строку.
//     Если значение не указано, то строка будет выдаваться на языке, определенного
//     для текущего пользователя.
// 
// Возвращаемое значение:
//   Строка - степень прописью (например - "метр в квадрате")
//
Функция СтепеньПрописью(Знач Объект, Знач ПоказательСтепени, Знач ОбъектГеометрический = Ложь, Знач КодЯзыка = "") Экспорт
	
	Если ПоказательСтепени = 0 Тогда
		Возврат "";
	Иначе
		Возврат СтрШаблон(
			РаботаСоСтрокамиЮникодКлиентСерверПовтИсп.ПолучитьСоответствиеШаблоновНаименованийСтепеней(
				ОбъектГеометрический, КодЯзыка).Получить(Число(ПоказательСтепени)),
			Объект
		);
	КонецЕсли;
	
КонецФункции

#КонецОбласти
