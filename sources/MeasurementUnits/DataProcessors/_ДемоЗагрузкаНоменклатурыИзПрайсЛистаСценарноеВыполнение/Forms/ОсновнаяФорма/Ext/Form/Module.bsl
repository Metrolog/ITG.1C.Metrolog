#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ДополнительнаяОбработкаСсылка) Тогда
		
		ХранилищеНастроек = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Параметры.ДополнительнаяОбработкаСсылка,
			"ХранилищеНастроек");
		Настройки = ХранилищеНастроек.Получить();
		
		Если Настройки <> Неопределено Тогда
			Настройки.Свойство("АдресФайла", АдресФайла);
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(АдресФайла) Тогда
		АдресФайла = "http://www.1c.ru/ftp/pub/pricelst/price_1c.zip";
	КонецЕсли;
	
	СформироватьСценарии();
	КлючСессии = Параметры.КлючСессии;
	Откуда = "ИзИнтернет";
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОткудаПриИзменении(Элемент)
	
	Элементы.СтраницыПолучениеФайла.ТекущаяСтраница = Элементы["Страница" + Откуда];
	
КонецПроцедуры

&НаКлиенте
Процедура АдресПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Не ПодключитьРасширениеРаботыСФайлами() Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Укажите путь к файлу прайс-листа'");
	ДиалогОткрытияФайла.Фильтр = НСтр("ru = 'Файл Microsoft Office Excel (*.xls)|*.xls|Архив (*.zip)|*.zip'");
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		АдресФайла = ДиалогОткрытияФайла.ПолноеИмяФайла;
		Если ЗначениеЗаполнено(Параметры.ДополнительнаяОбработкаСсылка) Тогда
			СохранитьНастройкиФормы(Параметры.ДополнительнаяОбработкаСсылка, АдресФайла);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресПредставлениеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	АдресФайлаСКлиента = "";
	АдресПредставление = "";
	
КонецПроцедуры

&НаКлиенте
Процедура АдресФайлаПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Параметры.ДополнительнаяОбработкаСсылка) И Откуда = "ИзИнтернет" Тогда
		СохранитьНастройкиФормы(Параметры.ДополнительнаяОбработкаСсылка, АдресФайла);
	КонецЕсли;
	СформироватьСценарии();
КонецПроцедуры

&НаКлиенте
Процедура АдресФайлаОчистка(Элемент, СтандартнаяОбработка)
	АдресФайла = "http://www.1c.ru/ftp/pub/pricelst/price_1c.zip";
КонецПроцедуры

&НаКлиенте
Процедура АдресПредставлениеПриИзменении(Элемент)
	СформироватьСценарии();
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	ОчиститьСообщения();
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Откуда = "СКлиента" Тогда
		АдресСценария = АдресСценарияСКлиента;
		СохраняемыеЗначения = Новый Структура("АдресАрхива", АдресФайлаСКлиента);
	Иначе
		АдресСценария = АдресОсновногоСценария;
		СохраняемыеЗначения = Неопределено;
	КонецЕсли;
	
	ДополнительныеОтчетыИОбработкиВБезопасномРежимеВызовСервера.ВыполнитьСценарийВБезопасномРежиме(
		КлючСессии, АдресСценария,, СохраняемыеЗначения);
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьСценарии()
	
	ОбъектОбработки = РеквизитФормыВЗначение("Объект");
	
	ПараметрыВыполнения = Новый Структура("ДополнительнаяОбработкаСсылка, АдресФайла, ВключатьЗагрузку");
	ПараметрыВыполнения.ДополнительнаяОбработкаСсылка = Параметры.ДополнительнаяОбработкаСсылка;
	
	ПараметрыВыполнения.АдресФайла = АдресФайла;
	ПараметрыВыполнения.ВключатьЗагрузку = Истина;
	Сценарий = ОбъектОбработки.СформироватьСценарий(Неопределено, ПараметрыВыполнения);
	АдресОсновногоСценария = ПоместитьВоВременноеХранилище(Сценарий, УникальныйИдентификатор);
	
	ПараметрыВыполнения.АдресФайла = АдресФайлаСКлиента;
	ПараметрыВыполнения.ВключатьЗагрузку = Ложь;
	Сценарий = ОбъектОбработки.СформироватьСценарий(Неопределено, ПараметрыВыполнения);
	АдресСценарияСКлиента = ПоместитьВоВременноеХранилище(Сценарий, УникальныйИдентификатор);
	
КонецФункции

&НаСервереБезКонтекста
Процедура СохранитьНастройкиФормы(ОбъектСсылка, АдресФайла)
	
	СохраняемоеЗначение = Новый Структура("АдресФайла", АдресФайла);
	
	ДополнительнаяОбработкаОбъект = ОбъектСсылка.ПолучитьОбъект();
	ДополнительнаяОбработкаОбъект.ХранилищеНастроек = Новый ХранилищеЗначения(СохраняемоеЗначение);
	ДополнительнаяОбработкаОбъект.Записать();
	
КонецПроцедуры

#КонецОбласти
