! -----------------------------------------------------------------------
! Грамматика обозначений единиц величин
! © Бетке С.С. sergey.s.betke@yandex.ru
! -----------------------------------------------------------------------

"Name"    = 'Грамматика обозначений единиц СИ'
"Version" = '0.1.0.0'
"Author"  = 'Sergei S. Betke'
"About"   = 'Грамматика предназначена для разбора обозначений производных единиц с выделением кратных и дробных приставок.'
| 'Обозначения по Постановлению Правительства РФ от 31.10.2009 №879.'

"Case Sensitive"    = True
"Character Mapping" = 'Unicode'
"Auto Whitespace"   = True

"Start Symbol"      = <UnitSymbol>

<UnitSymbol> ::= <UnitSymbolNumerator>
              | <UnitSymbolNumerator> FractionOperator <UnitSymbolDenominator>

{FractionOperatorChars} = ['/'] + {&2044} ! ⁄

FractionOperator = {FractionOperatorChars}
 
<UnitSymbolNumerator> ::= <UnitSpecialSymbolsWithPower>

<UnitSymbolDenominator> ::= <UnitSpecialSymbolsWithPower>

<UnitSpecialSymbolsWithPower> ::= <UnitSpecialSymbolWithPower> <MultiplicationOperator> <UnitSpecialSymbolsWithPower>
                               |  <UnitSpecialSymbolWithPower>

{MultiplicationOperator} = ['*'] + {&22C5} ! ⋅
MultiplicationOperator = {MultiplicationOperator}
<MultiplicationOperator> ::= MultiplicationOperator

<UnitSpecialSymbolWithPower> ::= <UnitSpecialSymbol><PowerWithPrefix>

<UnitSpecialSymbol> ::= <CyrillicUnitSpecialSymbol>
                     | AngularDegreeSymbol | AngularMinuteSymbol | AngularSecondSymbol
                     | PercentSign | PerMilleSign
                     | AngstromSign
                     | <CelsiusDegree>

{Dot} = [.]

<CyrillicUnitSpecialSymbol> ::= <CyrillicUnitSymbol>
                             | <CyrillicUnitSymbol> <CyrillicShorthand>
                             | <CyrillicUnitSymbol> <CyrillicShorthand> <CyrillicShorthand>
                             | <CyrillicShorthand> <CyrillicShorthand>
                             | <CyrillicShorthand> <CyrillicShorthand> <CyrillicShorthand>

<CyrillicUnitSymbol> ::= <CyrillicUnitSymbolPrefix><CyrillicBasicUnitSymbol>

<CyrillicBasicUnitSymbol> ::= CyrillicUnitSymbol

CyrillicUnitSymbol = {Cyrillic}+
                   
<CyrillicShorthand> ::= CyrillicShorthand

CyrillicShorthand = {Cyrillic}+{Dot}

{AngularDegree} = [°]
AngularDegreeSymbol = {AngularDegree}
{AngularMinute} = [′]
AngularMinuteSymbol = {AngularMinute}
{AngularSecond} = [″]
AngularSecondSymbol = {AngularSecond}

{PercentSign} = [%]
PercentSign = {PercentSign}
{PerMilleSign} = {&8240} ! ‰
PerMilleSign = {PerMilleSign}

{AngstromSign} = {&212B} + {&00C5} !Å Å
AngstromSign = {AngstromSign}

{CelsiusDegree} = {&2103} ! ℃
CelsiusDegree = {CelsiusDegree}

<CelsiusDegree> ::= CelsiusDegree | '°C'

<PowerWithPrefix> ::= PowerOperator <Power> | <Power> | <PowerSuperscript> | <>

<Power> ::= Number

{Sign} = [+-]
Number = {Sign}?{Digit}+
PowerOperator = [^]

<PowerSuperscript> ::= NumberSuperscript

{SignSuperscriptChars} = {&207B} + {&207A} ! ⁻⁺
{DigitSuperscript} = {&2070} + {&00B9} + {&00B2..&00B3} + {&2074..&2079}
NumberSuperscript = {SignSuperscriptChars}?{DigitSuperscript}+

<CyrillicUnitSymbolPrefix> ::= <>
                    | yotta | zetta | exa   | peta  | tera | giga | mega  | kilo | hecto | deca
                    | deci  | centi | milli | micro | nano | pico | femto | atto | zepto | yocto

yotta = 'И'
zetta = 'З'
exa   = 'Э'
peta  = 'П'
tera  = 'Т'
giga  = 'Г'
mega  = 'М'
kilo  = 'к'
hecto = 'г'
deca  = 'да'

deci  = 'д'
centi = 'с'
milli = 'м'
micro = 'мк'
nano  = 'н'
pico  = 'п'
femto = 'ф'
atto  = 'а'
zepto = 'з'
yocto = 'и'

