! -----------------------------------------------------------------------
! Грамматика обозначений единиц величин
! © Бетке С.С. sergey.s.betke@yandex.ru
! -----------------------------------------------------------------------

"Name"    = 'Грамматика обозначений единиц СИ'
"Version" = '0.1.0.0'
"Author"  = 'Sergei S. Betke'
"About"   = 'Грамматика предназначена для разбора обозначений производных единиц с выделением кратных и дробных приставок.'
| 'Обозначения по Постановлению Правительства РФ от 31.10.2009 №879.'

"Case Sensitive" = 'True'
"Character Mapping" = 'Unicode'
"Auto Whitespace" = 'True'

"Start Symbol" = <UnitSymbol>

<UnitSymbol> ::= <UnitSymbolNumerator>
              | <UnitSymbolNumerator> UnitSymbolsFractionOperator <UnitSymbolDenominator>

{UnitSymbolsFractionOperatorChars} = ['/'] + {&2044} ! ⁄

UnitSymbolsFractionOperator = {UnitSymbolsFractionOperatorChars}
 
<UnitSymbolNumerator> ::= <UnitSpecialSymbolsWithPower>

<UnitSymbolDenominator> ::= <UnitSpecialSymbolsWithPower>

<UnitSpecialSymbolsWithPower> ::= <UnitSpecialSymbolWithPower> UnitSymbolsMultiplicationOperator <UnitSpecialSymbolsWithPower>
                               | <UnitSpecialSymbolWithPower>

{UnitSymbolsMultiplicationOperatorChars} = ['*'] + {&22C5} ! ⋅

UnitSymbolsMultiplicationOperator = {UnitSymbolsMultiplicationOperatorChars}

<UnitSpecialSymbolWithPower> ::= <UnitSpecialSymbol> <PowerWithPrefix>

<UnitSpecialSymbol> ::= UnitSymbol
                     | AngularUnitSymbol
                     | <CelsiusDegree>

UnitSymbol = {Cyrillic}+
           
{AngularDegree} = [°]
{AngularMinute} = [′]
{AngularSecond} = [″]
{AngularUnitSymbol} = {AngularDegree} + {AngularMinute} + {AngularSecond}

AngularUnitSymbol = {AngularUnitSymbol}

{CelsiusDegree} = {&2103} ! ℃
CelsiusDegree = {CelsiusDegree}

<CelsiusDegree> ::= CelsiusDegree 
                 | '°C'

           
<PowerWithPrefix> ::= 
         | PowerPrefix <Power>
         | <Power>
         | <PowerSuperscript>

<Power> ::= Sign Power
         | Power

Sign = [+-]
     
Power = {Digit}+
      
PowerPrefix = [^]

<PowerSuperscript> ::= SignSuperscript PowerSuperscript
         | PowerSuperscript

{SignSuperscriptChars} = {&207B} + {&207A} ! ⁻⁺
                       
SignSuperscript = {SignSuperscriptChars}
     
{DigitSuperscript} = {&2070} + {&00B9} + {&00B2..&00B3} + {&2074..&2079}

PowerSuperscript = {DigitSuperscript}+
      




! {Whitespace} = {Space} + {NBSP}
             
!*

"Start Symbol" = <UnitSymbolOneWord>

!<UnitSymbolOneWord> ::= <Prefix> <UnitSymbolAllowingPrefixes>
<UnitSymbolOneWord> ::= <Prefix> <UnitSymbolAllowingPrefixes>
                     | <UnitSymbolAllowingPrefixes>
                     | <UnitSymbolDisallowingPrefixes>

!<UnitSymbolAllowingPrefixes> ::= <metr> | <gram> | <newton>
UnitSymbolOneWord = {Cyrillic}+
<UnitSymbolAllowingPrefixes> ::= UnitSymbolOneWord

<UnitSymbolDisallowingPrefixes> ::= <AngularDegree> | <AngularMinute> | <AngularSecond>
                                 | <CelsiusDegree>

<metr>   ::= 'м'
<gram>   ::= 'г'
<newton> ::= 'Н'

<AngularDegree> ::= '°'
<AngularMinute> ::= '′'
<AngularSecond> ::= '″'

CelsiusDegree = {&2103} ! ℃
<CelsiusDegree> ::= CelsiusDegree | '°C'


<Prefix> ::=
          | <milli>
          | <micro>
!          | <mega>
             
<milli> ::= 'м'
<micro> ::= 'мк' | 'µ'
!<mega>  ::= 'М'

*!
